/-
Copyright (c) 2025 Mitchell Horner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mitchell Horner
-/
import Mathlib.Analysis.Asymptotics.AsymptoticEquivalent
import Mathlib.Combinatorics.SimpleGraph.Extremal.Basic
import Mathlib.Data.Nat.Choose.Cast

/-!
# TurÃ¡n density

This files defines the **TurÃ¡n density** of a simple graph.

## Main definitions

* `SimpleGraph.turanDensity H` is the **TurÃ¡n density** of the simple graph `H`, defined as the
  limit of `extremalNumber (Fin n) H / n.choose 2` as `n` approaches `âˆ`.

* `SimpleGraph.tendsto_turanDensity` is the proof that `SimpleGraph.turanDensity` is well-defined.

* `SimpleGraph.isEquivalent_extremalNumber` is the proof that `extremalNumber (Fin n) H` is
  asymptotically equivalent to `turanDensity H * n.choose 2` as `n` approaches `âˆ`.
-/


open Asymptotics Finset Fintype Topology

namespace SimpleGraph

variable {V : Type*} {G H : SimpleGraph V}

section TuranDensity

lemma extremalNumber_div_choose_two_succ_le {n : â„•} (hn : 2 â‰¤ n) :
    (extremalNumber (Fin (n+1)) H / (n+1).choose 2 : â„)
      â‰¤ (extremalNumber (Fin n) H / n.choose 2 : â„) := by
  rw [div_le_iffâ‚€ (mod_cast Nat.choose_pos (by linarith)),
    extremalNumber_le_iff_of_nonneg (Fin (n+1)) H (by positivity)]
  intro G _ h
  rw [mul_comm, â†mul_div_assoc, le_div_iffâ‚€' (mod_cast Nat.choose_pos hn)]
  -- double-counting vertices not in edges
  let s := (univ Ã—Ë¢ G.edgeFinset).filter fun (v, e) â†¦ v âˆ‰ e
  -- counting over vertices
  have h_vert : #s â‰¤ extremalNumber (Fin n) H * (n+1) := by
    rw [card_filter, sum_product]
    conv =>
      enter [1, 2, x]
      rw [â†card_filter]
    conv_rhs =>
      rw [â†Fintype.card_fin (n+1), â†card_univ, mul_comm, â†smul_eq_mul, â†sum_const]
    apply sum_le_sum; intro i _
    let e : ({i}á¶œ : Set (Fin (n+1))) â‰ƒ Fin n := by
      apply Fintype.equivFinOfCardEq
      rw [Fintype.card_compl_set, Fintype.card_fin, card_unique]
      exact add_tsub_cancel_right n 1
    rw [â†edgeFinset_deleteIncidenceSet_eq_filter, â†card_edgeFinset_induce_compl_singleton,
      â†extremalNumber_eq_extremalNumber_of_iso e Iso.refl]
    apply le_extremalNumber
    contrapose! h
    rw [not_not] at h âŠ¢
    exact h.trans âŸ¨SubgraphIso.induce G {i}á¶œâŸ©
  -- counting over edges
  have h_edge : #s = #G.edgeFinset * (n-1) := by
    rw [card_filter, sum_product_right]
    conv =>
      enter [1, 2, y]
      rw [â†card_filter]
    simp_rw [Sym2.mem_toFinset, filter_not, filter_mem_eq_inter, â†sum_attach G.edgeFinset,
      univ_inter, â†compl_eq_univ_sdiff, card_compl, Fintype.card_fin, card_toFinset_mem_edgeFinset,
      sum_const, card_attach, smul_eq_mul, Nat.succ_sub_succ_eq_sub]
  rw [mul_comm (n.choose 2 : â„) _, Nat.cast_choose_two, â†mul_div_assoc,
    mul_comm ((n+1).choose 2 : â„) _, Nat.cast_choose_two, â†mul_div_assoc,
    div_le_div_iff_of_pos_right zero_lt_two, â†Nat.cast_pred (by positivity),
    â†Nat.cast_pred (by positivity), mul_comm (n : â„) _, â†mul_assoc, â†mul_assoc,
    add_tsub_cancel_right n 1, mul_le_mul_right (by positivity)]
  rw [h_edge] at h_vert; assumption_mod_cast

/-- The limit `extremalNumber (Fin n) H / n.choose 2` as `n` approaches `âˆ` exists. -/
lemma exists_tendsto_extremalNumber_div_choose_two (H : SimpleGraph V) :
    âˆƒ x, Filter.Tendsto (fun (n : â„•) â†¦ (extremalNumber (Fin n) H / n.choose 2 : â„))
      Filter.atTop (ğ“ x) := by
  let f := fun (n : â„•) â†¦ (extremalNumber (Fin n) H / n.choose 2 : â„)
  suffices h : âˆƒ x, Filter.Tendsto (fun (n : â„•) â†¦ f (n + 2)) Filter.atTop (ğ“ x) by
    simpa [Filter.tendsto_add_atTop_iff_nat 2] using h
  -- limit of antitone sequence bounded from below is infimum
  use â¨… n, f (n + 2)
  apply tendsto_atTop_ciInf
  Â· apply antitone_nat_of_succ_le
    intro n
    apply extremalNumber_div_choose_two_succ_le
    rw [le_add_iff_nonneg_left]
    exact Nat.zero_le n
  Â· simp_rw [bddBelow_def, Set.mem_range, forall_exists_index, forall_apply_eq_imp_iff]
    use 0; intro n
    positivity

/-- The **TurÃ¡n density** of a simple graph `H` is the limit of
`extremalNumber (Fin n) H / n.choose 2` as `n` approaches `âˆ`.

See `SimpleGraph.tendsto_turanDensity` for proof of existence. -/
noncomputable def turanDensity (H : SimpleGraph V) :=
  limUnder Filter.atTop
    fun (n : â„•) â†¦ (extremalNumber (Fin n) H / n.choose 2 : â„)

/-- The **TurÃ¡n density** of a simple graph `H` is well-defined. -/
theorem tendsto_turanDensity (H : SimpleGraph V) :
    Filter.Tendsto (fun (n : â„•) â†¦ (extremalNumber (Fin n) H / n.choose 2 : â„))
      Filter.atTop (ğ“ (turanDensity H)) := by
  have âŸ¨_, hâŸ© := exists_tendsto_extremalNumber_div_choose_two H
  rwa [â†Filter.Tendsto.limUnder_eq h] at h

/-- `extremalNumber (Fin n) H` is asymptotically equivalent to `turanDensity H * n.choose 2` as `n`
approaches `âˆ`. -/
theorem isEquivalent_extremalNumber {H : SimpleGraph V} (h : turanDensity H â‰  0) :
    (fun (n : â„•) â†¦ (extremalNumber (Fin n) H : â„))
      ~[Filter.atTop] (fun (n : â„•) â†¦ (turanDensity H * n.choose 2 : â„)) := by
  have hz : âˆ€á¶  (x : â„•) in Filter.atTop, turanDensity H * x.choose 2 â‰  0 := by
    rw [Filter.eventually_atTop]
    use 2; intro n hn
    simp [h, Nat.choose_eq_zero_iff, hn]
  rw [isEquivalent_iff_tendsto_one hz]
  have hÏ€ := tendsto_turanDensity H
  apply Filter.Tendsto.const_mul (1 / (turanDensity H) : â„) at hÏ€
  simp_rw [one_div_mul_cancel h] at hÏ€
  convert hÏ€
  field_simp [Pi.div_apply]

end TuranDensity

end SimpleGraph
